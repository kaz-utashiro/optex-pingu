#!/usr/bin/env perl
use v5.24;
use warnings;
use utf8;

use Data::Dumper;
use List::Util qw(pairs zip reduce);
use Term::ANSIColor::Concise qw(ansi_color);
$Term::ANSIColor::Concise::NO_RESET_EL = 1;

use constant {
    THB => "\N{UPPER HALF BLOCK}",
    BHB => "\N{LOWER HALF BLOCK}",
    LHB => "\N{LEFT HALF BLOCK}",
    RHB => "\N{RIGHT HALF BLOCK}",
    QUL => "\N{QUADRANT UPPER LEFT}",
    QUR => "\N{QUADRANT UPPER RIGHT}",
    QLL => "\N{QUADRANT LOWER LEFT}",
    QLR => "\N{QUADRANT LOWER RIGHT}",
    QULx => "\N{U+259F}",
    QURx => "\N{U+2599}",
    QLLx => "\N{U+259C}",
    QLRx => "\N{U+259B}",
    QULLR => "\N{QUADRANT UPPER LEFT AND LOWER RIGHT}",
    QURLL => "\N{QUADRANT UPPER RIGHT AND LOWER LEFT}",
    FB  => "\N{FULL BLOCK}",
};
my $color_re = qr/[RGBCMYKW]/i;

sub squeeze {
    map @$_, reduce {
	my $x = $a->[-1];
	if ($x && $x->[0] eq $b->[0] && $x->[1] eq $b->[1]) {
	    $x->[2]++;
	} else {
	    $b->[2] = 1;
	    push @$a, $b;
	}
	$a;
    } [], @_;
}

sub stringify {
    my($hi, $lo, $c) = @{+shift};
    $c //= 1;
    local $_ = $hi . $lo;
    my $r = $color_re;
    my($f, $b, $ch);
    if    (/($r)\1\1\1/x)  { ($f,$b)=($1,$2); $ch=FB; }
    elsif (/($r)\1\1(.)/x) { ($f,$b)=($1,$2); $ch=QLRx; }
    elsif (/($r)\1(.)\1/x) { ($f,$b)=($1,$2); $ch=QLLx; }
    elsif (/($r)\1(.) ./x) { ($f,$b)=($1,$2); $ch=THB; }
    elsif (/($r)(.)\1\1/x) { ($f,$b)=($1,$2); $ch=QURx; }
    elsif (/($r)(.)\1 ./x) { ($f,$b)=($1,$2); $ch=LHB; }
    elsif (/($r)(.) .\1/x) { ($f,$b)=($1,$2); $ch=QULLR; }
    elsif (/($r)(.) . ./x) { ($f,$b)=($1,$2); $ch=QUL; }
    elsif (/(.)($r)\2\2/x) { ($f,$b)=($2,$1); $ch=QULx; }
    elsif (/(.)($r)\2 ./x) { ($f,$b)=($2,$1); $ch=QURLL; }
    elsif (/(.)($r) .\2/x) { ($f,$b)=($2,$1); $ch=RHB; }
    elsif (/(.)($r) . ./x) { ($f,$b)=($2,$1); $ch=QUR; }
    elsif (/(.) .($r)\2/x) { ($f,$b)=($2,$1); $ch=BHB; }
    elsif (/(.) .($r) ./x) { ($f,$b)=($2,$1); $ch=QLL; }
    elsif (/(.) . .($r)/x) { ($f,$b)=($2,$1); $ch=QLR; }
    elsif (/(.) . . .()/x) {                  $ch=$1; }
    else                   { die }
    $f //= ''; $b //= '';
    my $s = $ch x $c;
    if ($f) {
	my $color = $f;
	$color .= "/$b" if $b =~ /$color_re/;
	$s = ansi_color($color, $s);
    }
    $s;
}

sub image {
    my $data = shift;
    my @data = grep !/^\s*#/, $data =~ /.+/g;
    @data % 2 and die;
    my @image;
    for (pairs @data) {
	my($hi, $lo) = @$_;
	my @data = squeeze zip [ $hi =~ /\X\X/g ], [ $lo =~ /\X\X/g ];
	my $line = join '', map stringify($_), @data;
	push @image, "$line\n";
    }
    wantarray ? @image : join '', @image;
}

our $data = do { local $/; <DATA> };

our $image = image($data);

if (__PACKAGE__ eq 'main') {
    use open IO => ':utf8', ':std';
    print $image;
}

__DATA__
# MIT License
# 
# Copyright (c) 2022 sheepla
# 
# Permission is hereby granted, free of charge, to any person obtaining a copy
# of this software and associated documentation files (the "Software"), to deal
# in the Software without restriction, including without limitation the rights
# to use, copy, modify, merge, publish, distribute, sublicense, and/or sell
# copies of the Software, and to permit persons to whom the Software is
# furnished to do so, subject to the following conditions:
# 
# The above copyright notice and this permission notice shall be included in all
# copies or substantial portions of the Software.
# 
# THE SOFTWARE IS PROVIDED "AS IS", WITHOUT WARRANTY OF ANY KIND, EXPRESS OR
# IMPLIED, INCLUDING BUT NOT LIMITED TO THE WARRANTIES OF MERCHANTABILITY,
# FITNESS FOR A PARTICULAR PURPOSE AND NONINFRINGEMENT. IN NO EVENT SHALL THE
# AUTHORS OR COPYRIGHT HOLDERS BE LIABLE FOR ANY CLAIM, DAMAGES OR OTHER
# LIABILITY, WHETHER IN AN ACTION OF CONTRACT, TORT OR OTHERWISE, ARISING FROM,
# OUT OF OR IN CONNECTION WITH THE SOFTWARE OR THE USE OR OTHER DEALINGS IN THE
# SOFTWARE.
#
  ......                ..          ......      ....        ....          ..................                      
  ......                ..          ......      ....        ....          ..................                      
  ......          ........                    ....    ....            ......  ..........    ....  ....            
  ......          ........                    ....    ....            ......  ..........    ....  ....            
  ......        ..............            ......                  ......  ..  ..........      kkkkkkkk            
  ......        ..............            kkkkkkkkkkkkkk          ......  ..  ..........   kkkkkkkkkkkkk          
..........    ................  ..  kkkkkkkkkkkkkkkkkkkkkkkkkk  ..........    ......    kkkkkkkkkkkkkkkkkk..    ..
..........    ................  kkkkkkkkkkkkkkkkkkkkkkkkkkkkkkkkkk........    ......  kkkkkkkkkkkkkkkkkkkkk.    ..
  ........  ................  kkkkkkkkkkkkkkkkkkkkkkkkkkkkkkkkkkkkkk  ..    ......  kkkkkkkkkkkkkkkkkkkkkkkk      
  ........  ................kkkkkkkkkkkkkkkkkkkkkkkkkkkkkkkkkkkkkkkkkk..    ...... kkkkkkkkkkkkkkkkkkkkkkkkk      
            .............. kkkkkkwwwwwwkkkkkkkkkkkkkkkkkkkkkkkkkkkkkkkkk........  kkkkkkkkkkkkkkkkkkkkkkkkkk      
            ..............kkkkkwwwwwwwwwwkkkkkkkkkkkkkkkkkkkkkkkkkkkkkkkk....... kkkkkkkkkkkkkkkkkkkkkkkkkk       
..        ..    ........ kkkkkwwwwKKKKwwwwkkkkkkkkkkkkkkkkkkkkkwwwwwwkkkkk......kkkkkkkkkkkkkkkkkkkkkkkkkkk       
..        ..    ........kkkkkkwwwwKKKKwwwwkkkkkkkkkkkkkkkkkkkwwwwwwwwwwkkkk.....kkkkkkkkkkkkkkkkkkkkkkkkkk        
      ....      ........kkkkkkkwwwwwwwwwwkkkkkkkkkkkkkkkkkkkwwwwKKKKwwwwkkk ..  .kkkkkkkkkkkkkkkkkkkkkkkkk        
      ....      ........kkkkkkkkkwwwwwwkkkkkkkrrrrrrrrkkkkkkwwwwKKKKwwwwkkkk..  ..kkkkkkkkkkkkkkkkkkkkkkk         
        ..               kkkkkkkkkkkkkkkkkrrrrrrrrrrrrrrrrkkkwwwwwwwwwwkkkkk      ..kkkkkkkkkkkkkkkkkkkkk         
        ..               kkkkkkkkkkkkkkkrrrrrrrrrrrrrrrrrrrrkkkwwwwwwkkkkkkk      ..  kkkkkkkkkkkkkkkkkkk         
      ........          ..kkkkkkkkkkkkkkrrrrrrrrrrrrrrrrrrrrkkkkkkkkkkkkkkk.           kkkkkkkkkkkkkkkkkk         
      ........          ..  kkkkkkkkkkkkkkrrrrrrrrrrrrrrrrkkkkkkkkkkkkkkkk..            kkkkkkkkkkkkkkkk          
    ..........            ..  kkkkkkkkkkkkkkkkrrrrrrrrkkkkkkkkkkkkkkkkkkk.               kkkkkkkkkkkkkkk..        
    ..........            ..    kkkkkkkkkkkkkkkkkkkkkkkkkkkkkkkkkkkkkkk ..                kkkkkkkkkkkkkk..        
............          ....  ..    kkkkkkkkkkkkkkkkkkkkkkkkkkkkkkkkkkk ..  ..            ..kkkkkkkkkkkkkk          
............          ....  .. kkkkkkkkkkkkkkkkkkkkkkkkkkkkkkkkkkk    ..  ..            .kkkkkkkkkkkkkk           
............                kkkkkkkkkkkkkkkkkkkkkkkkkkkkkkkkkkkkkkkk    ..            .. kkkkkkkkkkkkkk           
............             kkkkkkkkkkkkkkkkkkkkkkkkkkkkkkkkkkkkkkkkkkkkk  ..            ..kkkkkkkkkkkkkk            
............      ..  kkkkkkkkkkkkkkkkkkkkkkkkkkkkkkkkkkyyyyyykkkkkkkkkk      ....     kkkkkkkkkkkkkk             
............      ..kkkkkkkkkkkkkkkkkkkkkkkkkkkkkkkkyyyyyyWWWWWWWWkkkkkkkkk   ....   kkkkkkkkkkkkkkk              
......        ..  kkkkkkkkkkkkkkkkkkkkkkkkkkkkkkkkyyyyWWWWWWWWWWWWWWkkkkkkkkkk     kkkkkkkkkkkkkkk..              
......        ..kkkkkkkkkkkkkkkkkkkkkkkkkkkkkkkkyyyyWWWWWWWWWWWWWWWWWWkkkkkkkkkkkkkkkkkkkkkkkkkk  ..              
              kkkkkkkkkkkkkkkkkkkkkkkkkkkkkkkkyyyyWWWWWWWWWWWWWWWWWWWWWWkkkkkkkkkkkkkkkkkkkkk ..                  
             kkkkkkkkkkkkkkkkkkkkkkkkkkkkkkkyyyyWWWWWWWWWWWWWWWWWWWWWWWWWWkkkkkkkkkkkkkkkk    ..                  
            kkkkkkkkkkkkkkkkkkkkkkkkkkkkkkkyyyWWWWWWWWWWWWWWWWWWWWWWWWWWWWWWkkkkkkkkkkk ..                        
          kkkkkkkkkkkkkkkkkkkkkkkkkkkkkkkkyyyWWWWWWWWWWWWWWWWWWWWWWWWWWWWWWWWWkkkkkk    ..                        
         kkkkkkkkkkkkkkkkkkkkkkkkkkkkkkkkyyyWWWWWWWWWWWWWWWWWWWWWWWWWWWWWWWWWWWkkk  ................              
        kkkkkkkkkkkkkkkkkkkkkkkkkkkkkkkkyyyWWWWWWWWWWWWWWWWWWWWWWWWWWWWWWWWWWWWW    ................              
    ..kkkkkkkkkkkkkkkkkkkkkkkkkkkkkkkkkyyyWWWWWWWWWWWWWWWWWWWWWWWWWWWWWWWWWWWWWWW       ..................        
    .kkkkkkkkkkkkkkkkkkkkkkkkkkkkkkkkkyyyWWWWWWWWWWWWWWWWWWWWWWWWWWWWWWWWWWWWWWWWW      ..................        
  ..kkkkkkkkkkkkkkkkkkkkkkkkkkkkkkkkkyyyWWWWWWWWWWWWWWWWWWWWWWWWWWWWWWWWWWWWWWWWWWW             ........  ..  ..  
  ..kkkkkkkkkkkkkkkkkkkkkkkkkkkkkkkkyyyWWWWWWWWWWWWWWWWWWWWWWWWWWWWWWWWWWWWWWWWWWWW             ........  ..  ..  
